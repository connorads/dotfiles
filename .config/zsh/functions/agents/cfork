#!/usr/bin/env zsh
# cfork: fzf pick a Claude session and fork it
emulate -L zsh

local sid="${1:-}"

if [[ -z "$sid" ]]; then
  local selection
  selection=$({
    local f sid_name mtime project_dir project info count summary ts_fmt
    for f in ~/.claude/projects/*/*.jsonl(N); do
      [[ "$f" == */subagents/* ]] && continue
      sid_name=${f:t:r}
      (( ${#sid_name} < 20 )) && continue
      mtime=$(stat --format='%Y' "$f" 2>/dev/null || stat -f '%m' "$f")
      project_dir=${${f:h}:t}
      project=${project_dir#-}
      project=${project//-/\/}
      info=$(jq -n -r '
        reduce (inputs | objects) as $d ({c: 0, s: ""};
          if ($d.type == "user" or $d.type == "assistant") then .c += 1 else . end |
          if (.s == "" and $d.type == "user") then
            .s = (($d.message.content // "") |
              if type == "array" then
                [.[] | if type == "object" then .text // empty elif type == "string" then . else empty end][0] // ""
              elif type == "string" then .
              else "" end)[:80]
          else . end
        ) | "\(.c)\t\(.s)"
      ' < "$f" 2>/dev/null) || continue
      count=${info%%$'\t'*}
      summary=${info#*$'\t'}
      ts_fmt=$(date -d "@$mtime" '+%Y-%m-%d %H:%M:%S' 2>/dev/null \
        || date -r "$mtime" '+%Y-%m-%d %H:%M:%S')
      printf '%s\t%s\t%s\t%s\t%s\t%s msgs\n' "$mtime" "$sid_name" "$ts_fmt" "$summary" "$project" "$count"
    done | sort -t$'\t' -k1 -rn | cut -f2-
  } 2>/dev/null \
    | fzf --prompt="Fork session: " --with-nth=2.. --delimiter=$'\t' --no-sort)

  [[ -z "$selection" ]] && return
  sid=$(printf '%s' "$selection" | cut -f1)
fi

claude --resume "$sid" --fork-session

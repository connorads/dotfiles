# cplan: fzf pick a Claude plan and open in editor
local plandir="${HOME}/.claude/plans"
local projdir="${HOME}/.claude/projects"

[[ -d "$plandir" ]] || { echo "No plans directory"; return 1; }

# Build slug→project map from Claude session data
typeset -A _cplan_projmap
if [[ -d "$projdir" ]]; then
  # Collect git repo names for normalising worktree paths
  # e.g. beverlyhillsfilmfestival.com → beverlyhillsfilmfestival-com (dot→hyphen)
  local -a _gitrepos
  if [[ -d "${HOME}/git" ]]; then
    _gitrepos=(${(f)"$(ls "${HOME}/git" | sed 's/\./-/g' | awk '{print length, $0}' | sort -rn | cut -d' ' -f2-)"})
  fi

  local d dname project slug slugs repo
  for d in "$projdir"/*/; do
    dname=${d:t}
    project=""
    if [[ "$dname" =~ '-git-(.+)$' ]]; then
      project=${match[1]}
    elif [[ "$dname" =~ 'trees-(.+)$' ]]; then
      project=${match[1]}
      # Normalise worktree name to base repo (longest matching ~/git/ repo)
      for repo in $_gitrepos; do
        if [[ "$project" == "$repo" || "$project" == "$repo"-* ]]; then
          project=$repo
          break
        fi
      done
    fi
    [[ -z "$project" ]] && continue
    slugs=(${(f)"$(rg -oNI '[a-z]+-[a-z]+-[a-z]+\.md' --glob '*.jsonl' "$d" 2>/dev/null | sort -u)"})
    for slug in $slugs; do
      _cplan_projmap[${slug%.md}]=$project
    done
  done
fi

# Resolve project for a plan file: session map → content grep → fallback
_cplan_project() {
  local f="$1" slug=${${f:t}%.md}
  # 1. Session-based lookup
  if (( ${+_cplan_projmap[$slug]} )); then
    printf '%s\n' "${_cplan_projmap[$slug]}"
    return
  fi
  # 2. Content-based: match ~/git/<name> or ~/.trees/<name>
  local pmatch
  pmatch=$(grep -oEm1 '(~/|/Users/[^/]+/)(git|\.trees)/[^/<[:space:]][^/[:space:]]*' "$f" 2>/dev/null \
    | head -1 \
    | sed 's|.*/git/||; s|.*/.trees/||; s|/.*||')
  if [[ -n "$pmatch" ]]; then
    printf '%s\n' "$pmatch"
    return
  fi
  # 3. Home-dir config patterns → dotfiles
  if grep -qE '~/\.(config|zsh)|flake\.nix|nix-darwin|home-manager' "$f" 2>/dev/null; then
    printf '%s\n' "dotfiles"
    return
  fi
  printf '%s\n' "-"
}

local selection
selection=$(for f in "$plandir"/*.md; do
  [[ -f "$f" ]] || continue
  local title=$(head -1 "$f" | sed 's/^#\+ *//')
  local mod
  mod=$(stat --format='%Y' "$f" 2>/dev/null || stat -f '%m' "$f")
  local date=$(date -d "@$mod" '+%Y-%m-%d %H:%M' 2>/dev/null \
    || date -r "$mod" '+%Y-%m-%d %H:%M')
  local project=$(_cplan_project "$f")
  printf '%s\t%s\t%s\t%s\t%s\n' "$mod" "$f" "$date" "$project" "$title"
done | sort -t$'\t' -k1 -rn \
  | cut -f2- \
  | fzf --prompt="Open plan: " \
        --with-nth=2.. \
        --delimiter=$'\t' \
        --no-sort \
        --preview='head -40 {1}' \
        --preview-window=right:50%:wrap)

[[ -z "$selection" ]] && return

local file=$(echo "$selection" | cut -f1)
${EDITOR:-nvim} "$file"

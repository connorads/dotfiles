#!/usr/bin/env zsh
# pr-prompt: copy a PR review prompt to clipboard
# usage: pr-prompt <pr-url>
emulate -L zsh

local url=$1
if [[ $url != https://github.com/*/pull/* ]]; then
  print -u2 "usage: gh-pr-prompt <PR-url>"
  return 1
fi

local owner_repo pr_number
owner_repo=${url#https://github.com/}
owner_repo=${owner_repo%/pull/*}
pr_number=${url##*/}

{
  echo "Please review this PR: $url"
  echo
  echo "## Description"
  gh pr view "$url" -R "$owner_repo" --json body \
    --jq '.body // "No description provided"'

  echo
  echo "## Diff"
  gh pr diff "$url" -R "$owner_repo"

  echo
  echo "## Comments"
  gh api "repos/$owner_repo/issues/$pr_number/comments?per_page=100" --paginate \
    --jq '.[] | "\(.user.login) (\(.created_at)):\n\(.body)\n---"' 2>/dev/null || true

} | if [[ "$(uname)" == "Darwin" ]]; then pbcopy; else xclip -selection clipboard; fi

echo "PR review prompt copied to clipboard"

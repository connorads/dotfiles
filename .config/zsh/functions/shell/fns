# fns: search zsh functions and aliases with fzf
# Usage: fns [query]
emulate -L zsh

local query="$1"
local -a entries

# Collect functions from fpath subdirectories
local dir file name desc
for dir in ~/.config/zsh/functions ~/.config/zsh/functions/*(N/); do
  for file in "$dir"/*(N.); do
    name="${file:t}"
    [[ "$name" == _* ]] && continue
    desc=""
    local header
    header=$(head -1 "$file" 2>/dev/null)
    if [[ "$header" == \#\ * ]]; then
      desc="${header#\# }"
      # Strip "name: " prefix if present
      desc="${desc#*: }"
    fi
    entries+=("fn  ${name}  ${desc}")
  done
done

# Collect all loaded aliases (custom, oh-my-zsh plugins, etc.)
local aline aname aval
for aline in ${(f)"$(alias)"}; do
  aname="${aline%%=*}"
  aval="${aline#*=}"
  # Strip surrounding quotes
  aval="${aval#[\'\"]}"
  aval="${aval%[\'\"]}"
  entries+=("al  ${aname}  -> ${aval}")
done

[[ ${#entries[@]} -eq 0 ]] && { echo "no functions or aliases found" >&2; return 1; }

local selected
selected=$(printf '%s\n' "${entries[@]}" | sort -k2 | fzf --reverse \
  --query="$query" \
  --header='functions & aliases (enter to paste)' \
  --preview='
    type=$(echo {} | awk "{print \$1}")
    name=$(echo {} | awk "{print \$2}")
    if [ "$type" = "fn" ]; then
      for d in ~/.config/zsh/functions ~/.config/zsh/functions/*/; do
        f="$d/$name"
        [ -f "$f" ] && { cat "$f"; exit; }
      done
    fi
    # Alias: show expansion
    echo {} | cut -d" " -f3-
  ' \
  --preview-window=right:60%:wrap)

[[ -z "$selected" ]] && return 0

local selected_name
selected_name=$(echo "$selected" | awk '{print $2}')
print -z "$selected_name"

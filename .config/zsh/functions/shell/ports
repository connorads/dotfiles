#!/usr/bin/env zsh
# ports: inspect listening ports with fzf
# Usage: ports [-k]
#   -k  kill mode (select one or more ports to stop via killport)
emulate -L zsh

local kill_mode=0
[[ "$1" == "-k" ]] && kill_mode=1

local -a lines
lines=("${(@f)$(listening-lines)}")

if [[ ${#lines[@]} -eq 0 || (${#lines[@]} -eq 1 && -z "${lines[1]}") ]]; then
  print -u2 "no listening ports found"
  return 0
fi

local -a rows
local line
for line in "${lines[@]}"; do
  [[ -z "$line" ]] && continue

  local cmd pid addr port bind_addr exposure pid_info
  if [[ "$OSTYPE" == darwin* ]]; then
    cmd=$(echo "$line" | awk '{print $1}')
    pid=$(echo "$line" | awk '{print $2}')
    addr=$(echo "$line" | awk '{print $9}')
  else
    addr=$(echo "$line" | awk '{print $4}')
    pid_info=$(echo "$line" | awk '{print $6}')

    cmd="-"
    if [[ "$pid_info" == *\"* ]]; then
      cmd="${pid_info#*\"}"
      cmd="${cmd%%\"*}"
    fi

    pid="-"
    if [[ "$pid_info" == *"pid="* ]]; then
      pid="${pid_info#*pid=}"
      pid="${pid%%,*}"
    fi
  fi

  port="${addr##*:}"
  bind_addr="${addr%:*}"

  exposure="PUBLIC"
  case "$bind_addr" in
    127.*|"[::1]"|"::1"|localhost|"[localhost]"|"127.0.0.53%lo"|"127.0.0.54")
      exposure="LOCAL"
      ;;
    100.*|"[fd7a:115c:a1e0"*|"fd7a:115c:a1e0"*)
      exposure="TAILNET"
      ;;
  esac

  [[ -z "$pid" ]] && pid="-"
  [[ -z "$cmd" ]] && cmd="-"
  rows+=("$(printf '%-7s %-7s %-20s %-8s %s' "$port" "$pid" "$cmd" "$exposure" "$addr")")
done

[[ ${#rows[@]} -eq 0 ]] && { print -u2 "no listening ports found"; return 0; }

local mode_hint="enter: select"
if (( kill_mode )); then
  mode_hint="enter: TERM->KILL selected"
fi

local header="PORT    PID     PROCESS              EXPOSED  ADDRESS    | tab: mark  ${mode_hint}"
local selected
selected=$(printf '%s\n' "${rows[@]}" | sort -n -k1,1 | uniq | fzf --reverse --multi \
  --header="$header" \
  --bind='tab:toggle+down,btab:toggle+up' \
  --preview='
    port=$(echo {} | awk "{print \$1}")
    pid=$(echo {} | awk "{print \$2}")
    exposure=$(echo {} | awk "{print \$4}")
    addr=$(echo {} | awk "{print \$5}")

    echo "Port:      $port"
    echo "PID:       $pid"
    echo "Exposure:  $exposure"
    echo "Address:   $addr"

    case "$port" in
      7681) echo "Hint: webterm service (try: webtermdown $port)" ;;
      2468) echo "Hint: gigacode service (try: gigadown)" ;;
      3456) echo "Hint: companion service (try: companiondown)" ;;
      8000) echo "Hint: toad service (try: toaddown)" ;;
    esac

    if [ "$pid" != "-" ] && command -v ps >/dev/null 2>&1; then
      echo
      ps -p "$pid" -o pid,ppid,user,%cpu,%mem,etime,command 2>/dev/null
    fi
  ' \
  --preview-window=right:55%:wrap)

[[ -z "$selected" ]] && return 0

local -a sel_ports
sel_ports=(${(u)${(f)"$(printf '%s\n' "$selected" | awk '{print $1}')"}})

if (( kill_mode )); then
  killport "${sel_ports[@]}"
else
  printf '%s\n' "${sel_ports[@]}"
fi
